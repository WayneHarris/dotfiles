#!/bin/zsh
# shortcut to this dotfiles path is $ZSH
export ZSH="$HOME/.dotfiles"

# your project folder that we can `c [tab]` to
export PROJECTS="$HOME/Sites"

# your default editor
export EDITOR='nano'

# all of our zsh files
typeset -U config_files
config_files=($ZSH/*/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done

# load antigen and setup
source /opt/homebrew/share/antigen/antigen.zsh

antigen use oh-my-zsh

antigen bundle git
antigen bundle pip
antigen bundle zsh-users/zsh-syntax-highlighting

# apply the antigen configuration
antigen apply

# load starship
# eval "$(starship init zsh)"
source <(starship init zsh --print-full-init)

# load everything less the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source "$file"
done

if type brew &>/dev/null
then
  FPATH="$(brew --prefix)/share/zsh/site-functions:${FPATH}"
fi

# initialize autocomplete here, otherwise functions won't be loaded
autoload -Uz compinit
if [ ! -f ~/.zcompdump ] || [ $(date +'%j') != $(stat -f '%Sm' -t '%j' ~/.zcompdump) ]; then
  compinit -i
else
  compinit -C -i
fi

autoload -U colors; colors

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source "$file"
done

unset config_files

# use .localrc for secrets that you don't
# want in your public, versioned repo.
[[ -a ~/.localrc ]] && source ~/.localrc
